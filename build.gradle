buildscript {    // Configuration for building
  repositories {
    jcenter()    // Bintray's repository - a fast Maven Central mirror & more
    mavenCentral()
  }
  dependencies {
    classpath 'com.google.cloud.tools:appengine-gradle-plugin:1.+'    // Latest 1.x.x release
  }
}

repositories {   // repositories for Jar's you access in your code
  maven {
    url 'https://oss.sonatype.org/content/repositories/snapshots' // SNAPSHOT repository (if needed)
  }
  mavenCentral()
  jcenter()
}

apply plugin: 'java'                              // standard Java tasks
apply plugin: 'war'                               // standard Web Archive plugin
apply plugin: 'com.google.cloud.tools.appengine-standard'  // App Engine tasks
apply plugin: 'maven-publish'

dependencies {
    compile 'com.google.appengine:appengine-api-1.0-sdk:+'  // Latest App Engine Api's
    compile 'org.springframework.boot:spring-boot-starter-web:2.1.1.RELEASE'
    testCompile 'org.springframework.boot:spring-boot-starter-test:2.1.1.RELEASE'
    providedCompile 'org.slf4j:jul-to-slf4j:1.7.25'
    providedCompile 'javax.servlet:javax.servlet-api:3.1.0'
}

configurations.all {
    exclude group: 'org.springframework.boot', module: 'spring-boot-starter-tomcat'
    exclude group: 'org.slf4j', module: 'jul-to-slf4j'
}

version = '0.0.1-SNAPSHOT'
description = 'springboot-appengine-standard'
sourceCompatibility = '1.8'

publishing {
    publications {
        maven(MavenPublication) {
            from(components.java)
        }
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

appengine {  // App Engine tasks configuration
    run {      // local (dev_appserver) configuration (standard environments only)
        jvmFlags = ['-Ddatastore.backing_store=../../src/main/webapp/WEB-INF/appengine-generated/local_db.bin', '-Dappengine.fullscan.seconds=5']

        port = 8080             // default

    }
    
    deploy {   // deploy configuration\
        stopPreviousVersion = false  // default - stop the current version
        promote = false              // default - & make this the current version
        version = 'pr'
    }
}
